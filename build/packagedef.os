#Использовать fs
#Использовать 1commands

#Область ОбработчикиСобытий

Процедура ПередСборкой(Знач РабочийКаталог) Экспорт
	
	ПостроитьРешение();

	ДвоичныеФайлы = Новый Массив();
	ДвоичныеФайлы.Добавить("CoverageBSL.AddIn.dll");
	ДвоичныеФайлы.Добавить("DebugBSL.dll");
	ДвоичныеФайлы.Добавить("DebugClientBSL.dll");
	ДвоичныеФайлы.Добавить("CoverageBSL.dll");

	СкопироватьДвоичныеФайлы(РабочийКаталог, "net48", ДвоичныеФайлы);
	СкопироватьДвоичныеФайлы(РабочийКаталог, "net8.0", ДвоичныеФайлы);
	
	КопироватьФайл("build\package-loader.os", "package-loader.os");

КонецПроцедуры

Процедура ПослеСборки(РабочийКаталог, ПутьКФайлуПакета) Экспорт

	УдалитьФайлы("package-loader.os");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СкопироватьДвоичныеФайлы(РабочийКаталог, ВерсияСреды, ДвоичныеФайлы)
	
	КаталогДвоичныхДанных = ОбъединитьПути(РабочийКаталог, "bin", ВерсияСреды);
	ФС.ОбеспечитьПустойКаталог(КаталогДвоичныхДанных);

	КаталогСборкиДвоичныхДанных = ОбъединитьПути("src", "CoverageBSL.AddIn", "bin");
	КаталогИсточник = ОбъединитьПути(КаталогСборкиДвоичныхДанных, "Release", ВерсияСреды);

	Для каждого ДвоичныйФайл Из ДвоичныеФайлы Цикл
	 	ФайлИсточник = ОбъединитьПути(КаталогИсточник, ДвоичныйФайл);
	 	ФайлНазначение = ОбъединитьПути(КаталогДвоичныхДанных, ДвоичныйФайл);
	 	КопироватьФайл(ФайлИсточник, ФайлНазначение);
	КонецЦикла;

КонецПроцедуры

Процедура ПостроитьРешение()
	
	ФайлРешения = ОбъединитьПути("src", "CoverageBSL.sln");

	Команда = Новый Команда();
	Команда.ПоказыватьВыводНемедленно(Истина);
	Команда.УстановитьКоманду("dotnet");
	Команда.ДобавитьПараметр("msbuild");
	Команда.ДобавитьПараметр(ФайлРешения);
	Команда.ДобавитьПараметр("-p:Configuration=Release");

	Команда.Исполнить();

КонецПроцедуры

Функция ВерсияБиблиотеки()

	ФайлСвойстваПроекта = ОбъединитьПути("src", "Common", "project.props");

	РегулярноеВыражение = Новый РегулярноеВыражение("<ReleaseNumber .*>(.+)</ReleaseNumber>");

	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ФайлСвойстваПроекта, КодировкаТекста.UTF8);
	СвойстваСборкиТекст = ТекстовыйДокумент.ПолучитьТекст();

	Совпадение = РегулярноеВыражение.НайтиСовпадения(СвойстваСборкиТекст)[0];
	ВерсияБиблиотеки = Совпадение.Группы[1].Значение;

	Возврат ВерсияБиблиотеки;

КонецФункции

#КонецОбласти

Описание
	.Имя("coveragebsl")
	.Версия(ВерсияБиблиотеки())
	.Описание("Плагин замера покрытия для 1С:Предприятия")
	.ВерсияСреды("1.9.0")
	.ВключитьФайл("bin")
	.ВключитьФайл("examples")
	.ВключитьФайл("package-loader.os")
	.ВключитьФайл("README.md")
;
